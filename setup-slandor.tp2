BACKUP ~slandor/backup~
//AUTHOR ~Yovaneth @ DragonflightDesign@hotmail.com~
SUPPORT ~Yovaneth and Spellhold Studios at http://www.shsforums.net/forum/234-miscellaneous-released-mods/~

//MODDER setup_tra NONE

VERSION ~1.6~

README ~slandor/readme/slandor-readme-english.html~

ALWAYS

	/* --------------------------------------------------------------------------------------------- *
	 *    Convert strings to UTF-8 for BGEE/BG2EE                                                    *
	 *    No exception, everything needs to be converted                                             *
	 * -----------------------------------------------------------------------*--------------------- */
	ACTION_DEFINE_ARRAY y#noconvert BEGIN END
	// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
	ACTION_DEFINE_ARRAY y#reload BEGIN setup END

	LAF HANDLE_CHARSETS
		INT_VAR
			infer_charsets = 1
		STR_VAR
			tra_path = EVAL ~%MOD_FOLDER%/lang~
			noconvert_array = y#noconvert
			reload_array = y#reload
	END

END


AUTO_TRA ~slandor/lang/%s~

LANGUAGE ~Deutsch (Uebersetzung von Gerri)~
         ~deutsch~
		 ~slandor/lang/german/setup.tra~

LANGUAGE ~English (translation by Yovaneth)~
         ~English~
		 ~slandor/lang/english/setup.tra~

LANGUAGE ~Francais (Traduction : Le Marquis, des d'Oghmatiques)~
         ~french~
		 ~slandor/lang/french/setup.tra~

LANGUAGE ~Italiano (traduzione di Ilot)~
         ~italian~
		 ~slandor/italian/lang/setup.tra~

LANGUAGE ~Russian (translation by aerie.ru [prowler, hawkmoon, accolon])~
         ~russian~
		 ~slandor/lang/russian/setup.tra~

LANGUAGE ~Espanol (traduccion por Lisandro)~
         ~spanish~
		 ~slandor/lang/spanish/setup.tra~

LANGUAGE ~Polski (translation by Cahir)~
         ~polish~
         ~slandor/lang/polish/setup.tra~


/* ========================== *
 *  The Minotaur and Lilacor  *
 * ========================== */
BEGIN @0
DESIGNATED 0 LABEL ~the_minautor_and_lilacor~
REQUIRE_PREDICATE GAME_IS ~bg2 tob bgt bg2ee eet~ @1 /* You must have BG2, ToB, BGT, BG2:EE or EET installed to install Lucy the Wyvern Mod for BG2. */

// Compile dialogues
// -----------------
COMPILE ~slandor/dialog/slandor.d~

// Install creatures
// -----------------
COPY ~slandor/cre~ ~override~

COPY_EXISTING ~slandor.cre~ ~override~
	WRITE_SHORT 0x24  200	// Current Hit Points
	WRITE_BYTE  0x54  13	// Save versus death
	WRITE_BYTE  0x55  15	// Save versus wands
	WRITE_BYTE  0x56  14	// Save versus polymorph
	WRITE_BYTE  0x57  16	// Save versus breath attacks
	WRITE_BYTE  0x58  16	// Save versus spells
	WRITE_ASCII DIALOG ~slandor~ #8
	WRITE_ASCII SCRIPT_OVERRIDE ~~ #8
	WRITE_ASCII SCRIPT_DEFAULT ~slandor~ #8
	WRITE_BYTE  0x275 1		// gender (MALE)
BUT_ONLY

// Compile scripts
// ---------------
COMPILE ~slandor/scripts/slandor.baf~
COMPILE ~slandor/scripts/ysmyc01.baf~
COMPILE ~slandor/scripts/ysmyc02.baf~

// Workaround to be sure ar0418 has been assigned a script.
// --------------------------------------------------------
COPY_EXISTING ~ar0418.are~ ~override~
	WRITE_ASCII AREA_SCRIPT ar0418 #8
BUT_ONLY
ACTION_IF (FILE_EXISTS_IN_GAME ~ar0418.bcs~) BEGIN
	// Append to existing script
	EXTEND_TOP ~ar0418.bcs~ ~slandor/scripts/ar0418.baf~
END ELSE BEGIN
	// Create new script
	COMPILE ~slandor/scripts/ar0418.baf~
END
